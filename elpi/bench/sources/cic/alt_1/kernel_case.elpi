% ADDITIONAL CONVERSION FRAMEWORK FOR THE VALIDATION OF CASE %%%%%%%%%%%%%%%%%
% documentation in kernel.txt.

% convl_sn T1 S1 P1 M T2 S2 P2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type convl_sn term -> stack -> stack -> conversion_mode -> term -> stack -> stack -> prop.

convl_sn T1 S1 P1 M T2 S2 P2 :-
  steps T1 S1 P1 on_long (T\ S\ P\ convl_whnf T S P M T2 S2 P2).

% convl_dx T1 S1 P1 M T2 S2 P2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type convl_dx term -> stack -> stack -> conversion_mode -> term -> stack -> stack -> prop.

convl_dx T1 S1 P1 M T2 S2 P2 :-
  steps T2 S2 P2 on_long (T\ S\ P\ convl_whnf T1 S1 P1 M T S P).

% convl_main T1 S1 P1 M T2 S2 P2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type convl_main term -> stack -> stack -> conversion_mode -> term -> stack -> stack -> prop.

convl_main T1 S1 P1 M T2 S2 P2 :-
  steps T1 S1 P1 on_long (T\ S\ P\ convl_dx T S P M T2 S2 P2).

% convl_whnf T1 S1 P1 M T2 S2 P2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type convl_whnf term -> stack -> stack -> conversion_mode -> term -> stack -> stack -> prop.

% OUTPUT TYPE

% removing left product (on_case_type).
convl_whnf (prod W1 F1) [] [] (on_case_type W K S [V|SN] DX) U2 S2 [] :- !,
  pi x\ dec x W1 => def x on_no V [] [] =>
    convl_sn (F1 x) [] [] (on_case_type W K S SN DX) U2 S2 [].

% xi for right product (on_case_type).
convl_whnf (prod W1 F1) [] [] (on_case_type W K S [] [_|DX]) (prod W2 F2) [] [] :- !,
  conv_eq W1 W2, pi x\ dec x W1 =>
    convl_main (F1 x) [] [] (on_case_type W K S [] DX) (F2 x) [] [].

% final checks (on_case_type).
convl_whnf (sort YI) [] [] (on_case_type W1 K SN1 [] []) (prod W2 F2) [] [] :-
  steps W2 [] [] on_long
    (W\ S\ P\ sigma SN\ sigma DX\
      W = W1, P = [], split S K SN DX, conv_stacks SN1 SN
    ),
  pi x\ dec x W2 =>
    convl_dx (sort YI) [] [] on_case_proj (F2 x) [] [].

% projectability (on_case_proj)
% note: we do nothing for now ...
convl_whnf (sort YI) [] [] on_case_proj (sort YO) [] [] :- true.

% BRANCHES

% removing left product (on_case_branch).
convl_whnf (prod W1 F1) [] [] (on_case_branch V0 K U0 [V|SN]) U2 S2 [] :- !,
  pi x\ dec x W1 => def x on_no V [] [] =>
    convl_sn (F1 x) [] [] (on_case_branch V0 K U0 SN) U2 S2 [].

% xi for right product (on_case_branch).
convl_whnf (prod W1 F1) [] [] (on_case_branch V0 K U0 []) (prod W2 F2) [] [] :- !,
  conv_eq W1 W2, pi x\ dec x W1 =>
    convl_main (F1 x) [] [] (on_case_branch (appl V0 x) K U0 []) (F2 x) [] [].

% final checks (on_case_branch).
convl_whnf _ S1 [] (on_case_branch V K U []) U2 S2 [] :-
  split S1 K _ DX, append DX [V] DXV, conv_main U DXV [] on_eq U2 S2 [].

% conv_case_type T1 W K SN DX T2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type conv_case_type term -> term -> int -> stack -> stack -> term -> prop.

conv_case_type T1 W K SN DX T2 :- convl_main T1 [] [] (on_case_type W K SN SN DX) T2 [] [].

% conv_case_branch T1 V0 K SN U0 T2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type conv_case_branch term -> term -> int -> stack -> term -> term -> prop.

conv_case_branch T1 V0 K SN U0 T2 :-
  convl_main T1 [] [] (on_case_branch V0 K U0 SN) T2 [] [].
