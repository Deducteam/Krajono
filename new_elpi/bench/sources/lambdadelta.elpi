% Validator for \lambda\delta version 3 --------------------------------------

% sort: S := user defined
%
% Layer: L := user defined
%
% Closed term: T, U, V, W ::= sort   S
%                          |  void     F where F: T => T
%                          |  abbr   T F where F: T => T
%                          |  abst L T F where F: T => T 
%                          |  appl   T T
%                          |  cast   T T
%
% Global environment: G ::= gtop
%                        |  gdef T F where F: T => G
%                        |  gdec T F where F: T => G
%
% RTM mode: M, N := m+0 | m+1 | m+y 
%
% Predicates: flat M       = M = m+0 or M = m+y
%             rtm1 M T M T = reduction and type machine (single step)
%             rtm  M T M T = reduction and type machine (multiple step)
%             conv M T M T = conversion by levels
%             comp M T M T = comparator by levels
%             tv   T       = validity for terms
%             gv   G       = validity for global environments
%
% Parameters: ssucc S S = successor for sort hierarchy
%             lzero L   = zero layer
%             lpos  L   = positive layer
%             lpred L L = predeccessor for layers
%  
% Constants: m+0 = typing not allowed in RTM
%            m+1 = typing required in RTM
%            m+y = typing allowed in RTM (version 2)
%            a+r = restricted applicability (version 1)
%            a+x = extended applicability (version 2)

% REDUCTION AND TYPE MACHINE %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

flat m+0.

% version 2
flat m+y.

rtm1 M1 (appl V T1) M2 (appl V T2) :- rtm1 M1 T1 M2 T2, $print rtm1_appl. 

% local delta
rtm1 M1 (abbr V T1) M2 (abbr V T2) :- pi x\ rtm1 M x M V => rtm1 M1 (T1 x) M2 (T2 x), $print rtm1_delta. 

rtm1 M1 (void T1) M2 (void T2) :- pi x\ rtm1 M1 (T1 x) M2 (T2 x), $print rtm1_void. 

% local l
rtm1 m+1 (abst L W T1) m+0 (abst L W T2) :- lzero L,
                                            pi x\ rtm1 m+1 x m+0 W =>
                                            rtm1 m+1 (T1 x) m+0 (T2 x), $print rtm1_l.

% beta, epsilon, updated for version 3
rtm1 M (appl V (abst L W T)) M (abbr V T) :- lpos L, $print rtm1_beta.

% upsilon (version 3) 
rtm1 M (abst L W T) M (void T) :- flat M, lzero L, $print rtm1_upsilon.

% theta
rtm1 M (appl V1 (abbr V2 T)) M (abbr V2 x\ (appl V1 (T x))) :- $print rtm1_theta_abbr.

% theta
rtm1 M (appl V (void T)) M (void x\ (appl V (T x))) :- $print rtm1_theta_void.

% epsilon
rtm1 M (cast U T) M T :- flat M, $print rtm1_epsilon.

% e
rtm1 m+1 (cast U T) m+0 U :- $print rtm1_e.

% s
rtm1 m+1 (sort S1) m+0 (sort S2) :- ssucc S1 S2, $print rtm1_s.

% x (version 3)
rtm1 m+1 (abst L1 W T) m+1 (abst L2 W T) :- lpred L1 L2, $print rtm1_x.

rtm M T M T :- $print rtm_refl.

rtm M1 T1 M2 T2 :- rtm1 M1 T1 M T, rtm M T M2 T2, $print rtm_step.

% COMPARATOR %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

conv M1 U1 M2 U2 :- rtm M1 U1 N1 T1, rtm M2 U2 N2 T2, comp N1 T1 N2 T2, $print conv.

comp N T N T :- $print comp_refl.

comp N1 (appl V1 T1) N2 (appl V2 T2) :- conv N1 T1 N2 T2, conv m+0 V1 m+0 V2, $print comp_appl.

% local l updated for version 2 and 3
comp N1 (abst L W1 T1) N2 (abst L W2 T2) :- lpos L, conv m+0 W1 m+0 W2,
                                            pi x\ rtm1 m+1 x m+0 W2 => rtm1 m+y x m+y W2 =>
                                            conv N1 (T1 x) N2 (T2 x), $print comp_abst.

% local delta
comp N1 (abbr V T) N2 T2 :- pi x\ rtm1 M x M V => comp N1 (T x) N2 T2, $print comp_abbr_sn.

% local delta
comp N1 T1 N2 (abbr V T) :- pi x\ rtm1 M x M V => comp N1 T1 N2 (T x), $print comp_abbr_dx.

comp N1 (void T) N2 T2 :- pi x\ comp N1 (T x) N2 T2, $print comp_void_sn.

comp N1 T1 N2 (void T) :- pi x\ comp N1 T1 N2 (T x), $print comp_void_dx.

% VALIDITY FOR TERMS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

tv (sort N) :- $print tv_sort.

tv (void T) :- pi x\ tv (T x), $print tv_void.

% local delta
tv (abbr V T) :- tv V, pi x\ tv x => rtm1 M x M V => tv (T x), $print tv_abbr.

% local l updated for version 2 and 3
tv (abst L W T) :- tv W, pi x\ tv x => rtm1 m+1 x m+0 W => rtm1 m+y x m+y W => tv (T x), $print tv_abst.

% restricted applicability condition (version 1) updated for version 3
tv (appl V T) :- a+r, tv V, tv T, rtm M T m+0 (abst L W U), lpos L, conv m+1 V m+0 W, $print tv_appl_r.

%extended applicability condition (version 2) updated for version 3
tv (appl V T) :- a+x, tv V, tv T, rtm m+y T m+y (abst L W U), lpos L, conv m+1 V m+0 W, $print tv_appl_x.

tv (cast U T) :- tv U, tv T, conv m+1 T m+0 U, $print tv_cast.

% VALIDITY FOR GLOBAL CONTEXTS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

gv gtop. 

% global delta
gv (gdef T G) :- tv T, pi x\ tv x => rtm1 M x M T => gv (G x).

% global l updated for version 2
gv (gdec U G) :- tv U, pi x\ tv x => rtm1 m+1 x m+0 U => rtm1 m+y x m+y U => gv (G x).

% Additional matter ----------------------------------------------------------

% LAYERS FOR PTS, LAMBDA-INFINITY, AUT-68, AUT-QE %%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Constants: l+0 = hyper Pi
%            l+1 = Pi
%            l+2 = lambda
%            l+y = lambda-infinity

lzero l+0. 

lpos l+1. 

lpos l+2. 

lpos l+y. 

lpred l+1 l+0.

lpred l+2 l+1.

% TESTS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

test_sta :- ssucc s1 s2 => conv m+1 (sort s1) m+0 (sort s2).

test_conv :- conv m+0 (appl w1 (abst l+y w2 x\ (abst l+y x y\ y))) m+0 (abst l+y w1 x\ x).

test_tv_11 :- a+r => ssucc s1 s2 => ssucc s2 n3 => tv (appl (sort s1) (abst l+y (sort s2) x\ x)).

test_tv_12 :- a+r => tv w => tv (abst l+y w x\ cast w x).

test_tv_13 :- a+r => tv w => tv (cast (abst l+y w x\ w) (abst l+y w x\ x)).

% does fail with resticted applicability
test_tv_2 :- a+x => ssucc s1 s2 =>
             tv (abst l+y (abst l+y (sort s2) x\ (sort s2)) x\ (abst l+y x y\ appl (sort s1) y)).

% first line of the grundlagen: requires version 3
test_tv_3 :- a+r =>
             tv (abst l+y (sort s0) a\ (abst l+y (sort s0) b\ (cast (sort s0) (abst l+1 a z\ b)))).

main :- test_sta, test_conv, test_tv_11, test_tv_12, test_tv_13, test_tv_2, test_tv_3.
