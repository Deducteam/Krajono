% LIBRARY OF OPERATIONS ON LISTS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

append [] YS YS.
append [X|XS] YS [X|ZS] :- append XS YS ZS.

rev_append [] YS YS.
rev_append [X|XS] YS ZS :- rev_append XS [X|YS] ZS.

nth [X|_] 0 X :- !.
nth [_|XS] M X :- N is M - 1, nth XS N X.

split ZS 0 [] ZS :- !.
split [Z|ZS] M [Z|XS] YS :- N is M - 1, split ZS N XS YS.

subst [X|ZS] 0 R [Y|ZS] :- !, R X Y.
subst [Z|XS] M R [Z|YS] :- N is M - 1, subst XS N R YS.

forall [] _.
forall [X|XS] R :- R X, forall XS R.

forall2 [] [] _.
forall2 [X|XS] [Y|YS] R :- R X Y, forall2 XS YS R.

forall3 [] [] [] _.
forall3 [X|XS] [Y|YS] [Z|ZS] R :- R X Y Z, forall3 XS YS ZS R.

mem [X|_] X :- !.
mem [_|XS] X :- mem XS X.

fold_left [] _ Y Y.
fold_left [X|XS] R Y Z :- fold_left XS R (R Y X) Z.
