% validation of match construction.
% PTS_inductives invokes conv_match_type and conv_match_branch

% HELPERS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

type conv*_sn term -> stack -> conversion_mode -> term -> stack -> prop.
conv*_sn T1 S1 M T2 S2 :- whd* T1 S1 (_\ T\ S\ comp* T S M T2 S2).

type conv*_dx term -> stack -> conversion_mode -> term -> stack -> prop.
conv*_dx T1 S1 M T2 S2 :- whd* T2 S2 (_\ T\ S\ comp* T1 S1 M T S).

type conv* term -> stack -> conversion_mode -> term -> stack -> prop.
conv* T1 S1 M T2 S2 :- whd* T1 S1 (_\ T\ S\ conv*_dx T S M T2 S2).

type comp* term -> stack -> conversion_mode -> term -> stack -> prop.

% OUTPUT TYPE %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% removing left product (on_match_type).
comp* (arr W1 F1) [] (on_match_type W K S [V|SN] DX) U2 S2 :- !,
  pi x\ val x W1 V NF => conv*_sn (F1 x) [] (on_match_type W K S SN DX) U2 S2.

% xi for right product (on_match_type).
comp* (arr W1 F1) [] (on_match_type W K S [] [_|DX]) (arr W2 F2) [] :- !,
  conv W1 W2,
  pi x\ conv* (F1 x) [] (on_match_type W K S [] DX) (F2 x) [].

% final checks (on_match_type).
comp* (sort YI) [] (on_match_type W1 K SN1 [] []) (arr W2 F2) [] :-
  whd* W2 []
    (_\ W\ S\ sigma SN\ sigma DX\
      W = W1, split S K SN DX, forall2 SN1 SN conv
    ),
  pi x\ conv*_dx (sort YI) [] on_match_proj (F2 x) [].

% projectability (on_match_proj)
% note: we do nothing for now ...
comp* (sort YI) [] on_match_proj (sort YO) [] :- true.

type conv_match_type term -> term -> int -> stack -> stack -> term -> prop.
conv_match_type T1 W K SN DX T2 :- conv* T1 [] (on_match_type W K SN SN DX) T2 [].

% BRANCHES %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% removing left arruct (on_match_branch).
comp* (arr W1 F1) [] (on_match_branch V0 K U0 [V|SN]) U2 S2 :- !,
  pi x\ val x W1 V NF => conv*_sn (F1 x) [] (on_match_branch V0 K U0 SN) U2 S2.

% xi for right arruct (on_match_branch).
comp* (arr W1 F1) [] (on_match_branch V0 K U0 []) (arr W2 F2) [] :- !,
  conv W1 W2,
  pi x\ conv* (F1 x) [] (on_match_branch (app V0 x) K U0 []) (F2 x) [].

% final checks (on_match_branch).
comp* _ S1 (on_match_branch V K U []) U2 S2 :-
  split S1 K _ DX, append DX [V] DXV, comp U DXV eq U2 S2.

type conv_match_branch term -> term -> int -> stack -> term -> term -> prop.
conv_match_branch T1 V0 K SN U0 T2 :-
  conv* T1 [] (on_match_branch V0 K U0 SN) T2 [].
